plugins {
  id 'org.springframework.boot' version '3.2.3'
  id 'io.spring.dependency-management' version '1.1.4'
  id 'com.diffplug.spotless' version '6.25.0'
  id "org.sonarqube" version '3.0'
  id 'jacoco'
  id 'java'
}

ext {
  springdocVersion = '2.4.0'
}

group = 'com.josdem.fruitypedia'
version = '1.2.0'


java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

spotless {
  java {
    target fileTree('.') {
      include 'src/**/*.java'
      exclude '/build/**'
    }
    googleJavaFormat()
    removeUnusedImports()
    endWithNewline()
  }
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-web'
  implementation 'org.springframework.boot:spring-boot-starter-actuator'
  implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'
  implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:$springdocVersion"
  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
  testImplementation 'org.mockito:mockito-junit-jupiter'
  testCompileOnly 'org.projectlombok:lombok'
  testAnnotationProcessor "org.projectlombok:lombok"
}

jacocoTestReport {
  reports {
    xml.required = true
    html.required = true
    html.outputLocation = layout.buildDirectory.dir("${buildDir}/jacocoHtml")
  }
}

test {
  useJUnitPlatform()

  reports {
    html.required = true
  }
}

springBoot {
  buildInfo()
}

bootRun {
  systemProperties = System.properties
}

test.dependsOn("spotlessApply")